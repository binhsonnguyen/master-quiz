buildscript {
    dependencies {
        classpath 'mysql:mysql-connector-java:8.0.11'
    }
}

plugins {
    id 'java'
    id 'war'
    id 'jacoco'
    id "org.flywaydb.flyway" version '5.1.4'
}

def SPRING_VERSION = '4.3.19.RELEASE'

group 'mst.codegym'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // Spring's core
    compile group: 'org.springframework', name: 'spring-core', version: SPRING_VERSION

    // Lombok's Buider
    compile group: 'org.projectlombok', name: 'lombok', version: '1.18.2'

    // MVC
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    compile group: 'org.springframework', name: 'spring-webmvc', version: SPRING_VERSION
    compile group: 'taglibs', name: 'standard', version: '1.1.2'
    compile group: 'org.thymeleaf', name: 'thymeleaf-spring4', version: '3.0.4.RELEASE'
    compile group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-springsecurity4', version: '3.0.2.RELEASE'

    // Persistence
    compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.11'
    compile group: 'org.springframework.data', name: 'spring-data-jpa', version: '1.11.12.RELEASE'
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.3.0.Final'
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '5.3.0.Final'
    compile group: 'org.springframework', name: 'spring-orm', version: SPRING_VERSION

    // Security
    compile group: 'org.springframework.security', name: 'spring-security-web', version: '4.2.8.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-config', version: '4.2.8.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-taglibs', version: '4.2.8.RELEASE'

    // Logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    // Testing
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
    testCompile group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.2.0'
    testCompile group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.2.0'
    testCompile group: 'org.springframework', name: 'spring-test', version: SPRING_VERSION
    testCompile('com.github.sbrannen:spring-test-junit5:1.2.0')
    compile group: 'org.jacoco', name: 'jacoco-maven-plugin', version: '0.8.1'

    // Mocking
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'

    // DB Migration
    compile group: 'org.flywaydb', name: 'flyway-core', version: '5.1.4'
}

flyway {
    configFiles = ['flyway.conf']
}

test {

    useJUnitPlatform()

    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
    }
    afterTest { desc, result ->
        println "Testing ${desc.name} [${desc.className}]: ${result.resultType}"
    }
    reports {
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}
